#version 450 core
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_control_flow_attributes : enable

layout(binding=0) buffer InputA { vec4 x[]; } inputA;
layout(binding=1) buffer InputB { vec4 x[]; } inputB;
layout(binding=2) buffer Output { vec4 x[]; } outputO;

/*
    "TILE_M=[2|4|8|16|32]"
    "TILE_N=[64|128]"
    "TILE_K=[4|8]"
    "WG_X=16"
    "WG_Y=1"
*/
const uint TILE_M = 8;
const uint TILE_N = 64;
const uint TILE_K = 4;
const uint WG_X = 16;
const uint WG_Y = 1;

layout(local_size_x = WG_X, local_size_y = WG_Y, local_size_z = 1) in;

// 特化常量，在 vkCreateComputePipelines 中通过 pSpecializationInfo 指定。
// constant_id 对应偏移量，以0为首地址往后按数据类型内存占用量进行偏移
layout(constant_id = 0) const uint M = 1;
layout(constant_id = 1) const uint N = 1;
layout(constant_id = 2) const uint K = 1;

const uint strideA = K;
const uint strideB = N;
const uint strideC = N;

const uint C_ROWS = TILE_M / WG_Y;
const uint C_COLS = TILE_N / (4*WG_X);

// 坐标 转 偏移量
uint coordToOffset(uint i, uint j, uint stride)
{
    return (stride * i + j);
}

/*
    const uvec3 gl_WorkGroupSize: contains the size of the workgroup operated on by a compute shader
    in uvec3 gl_NumWorkGroups: contains the number of workgroups that have been dispatched to a compute shader

    in uvec3 gl_WorkGroupID: contains the index of the workgroup currently being operated on by a compute shader
    in uvec3 gl_LocalInvocationID: (线程局部下标) contains the index of work item currently being operated on by a compute shader

    in uvec3 gl_GlobalInvocationID: (线程全局下标) contains the global index of work item currently being operated on by a compute shader
    in uint gl_LocalInvocationIndex: (线程局部一维线性下标，上面的是三维) contains the local linear index of work item currently being operated on by a compute shader
*/

/*
    
    分块矩阵乘法

*/
void main()
{
    uvec2 gID = gl_WorkGroupID.xy;
    uvec2 laneId = gl_LocalInvocationID.xy;
    vec4 C[C_ROWS][C_COLS];
    vec4 B[TILE_K][C_COLS];

    // Initialize result to zero
    [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            C[i][j] = vec4(0.f, 0.f, 0.f, 0.f);
        }
    }

    for (uint k = 0; k < K; k+=TILE_K) {
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
          [[unroll]] for (uint i = 0; i < TILE_K; ++i) {
            uint gj = gID.x * (TILE_N / 4) + laneId.x +j*WG_X;
            uint gk = k+i;
            B[i][j] = inputB.x[coordToOffset(gk, gj, strideB/4)];
          }
        }

        [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
          uint gi = gID.y * TILE_M + laneId.y + i*WG_Y;
          uint gk = k/4;
          [[unroll]] for (uint kk = 0; kk < TILE_K/4; kk++) {
            vec4 A = inputA.x[coordToOffset(gi, gk+kk, strideA/4)];
            [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
              C[i][j] += vec4(A.x, A.x, A.x, A.x)*B[0+4*kk][j];
              C[i][j] += vec4(A.y, A.y, A.y, A.y)*B[1+4*kk][j];
              C[i][j] += vec4(A.z, A.z, A.z, A.z)*B[2+4*kk][j];
              C[i][j] += vec4(A.w, A.w, A.w, A.w)*B[3+4*kk][j];
            }
          }
        }
    }

    [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            uint gi = gID.y * TILE_M + laneId.y + i*WG_Y;
            uint gj = gID.x * (TILE_N / 4) + laneId.x +j*WG_X;
            outputO.x[gi * strideC/4 + gj] = C[i][j];
        }
    }
}
